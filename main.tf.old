######################
# VPC
######################

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "~> 5.0"

  name = "${var.cluster_name}-vpc"
  cidr = "10.0.0.0/16"

  azs             = ["${var.region}a", "${var.region}b"]
  private_subnets = ["10.0.1.0/24", "10.0.2.0/24"]
  public_subnets  = ["10.0.101.0/24", "10.0.102.0/24"]

  enable_nat_gateway = true
  single_nat_gateway = true

  tags = {
    Environment = var.environment
    Project     = "hartree-eks-demo"
  }
}

######################
# EKS Cluster
######################

module "eks" {
  source  = "terraform-aws-modules/eks/aws"
  version = "~> 20.0"

  depends_on = [module.vpc]

  cluster_name    = var.cluster_name
  cluster_version = "1.30"

  vpc_id     = module.vpc.vpc_id
  subnet_ids = module.vpc.private_subnets

  # Keep things simple + cheap
  cluster_endpoint_public_access  = true
  cluster_endpoint_private_access = false

  enable_irsa = true

  # Single tiny nodegroup, using SPOT to cut costs
  eks_managed_node_groups = {
    demo = {
      desired_size = 1
      min_size     = 1
      max_size     = 1

      capacity_type  = "SPOT"
      ami_type      = "AL2023_x86_64_STANDARD"
      instance_types = ["t3.micro"]

      labels = {
        role = "demo"
      }

      tags = {
        Environment = var.environment
        Project     = "hartree-eks-demo"
      }
    }
  }

  tags = {
    Environment = var.environment
    Project     = "hartree-eks-demo"
  }
}

module "eks_auth" {
  source  = "terraform-aws-modules/eks/aws//modules/aws-auth"
  version = "~> 20.0"

  depends_on = [module.eks]

  providers = {
    kubernetes = kubernetes.eks
  }

  aws_auth_users = [
    {
      userarn  = "arn:aws:iam::<ACCOUNT_ID>:user/tf-admin"
      username = "tf-admin"
      groups   = ["system:masters"]
    }
  ]
}

